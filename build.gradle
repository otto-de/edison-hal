plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'org.jreleaser' version '1.17.0'
}

version = '2.2.1-SNAPSHOT'
group = 'de.otto.edison'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    api("com.fasterxml.jackson.core:jackson-databind:2.19.1")
    implementation("com.damnhandy:handy-uri-templates:2.1.8")
    implementation("org.slf4j:slf4j-api:2.0.17")
    implementation('ch.qos.logback:logback-classic:1.5.18')

    testImplementation("junit:junit:4.13.2")
    testImplementation("org.hamcrest:hamcrest:3.0")
    testImplementation("org.mockito:mockito-core:5.17.0")
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

java {
    targetCompatibility = JavaVersion.VERSION_17
    sourceCompatibility = JavaVersion.VERSION_17
    withJavadocJar()
    withSourcesJar()
}

jreleaser {
    project {
        snapshot {
            fullChangelog = true
        }
    }
    gitRootSearch = true
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            github {
                app {
                    snapshotSupported = true
                    active = 'ALWAYS'
                    url = "https://maven.pkg.github.com/otto-de/edison-hal"
                    stagingRepository('build/staging-deploy')
                }
            }
            mavenCentral {
                app {
                    snapshotSupported = false
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')

                    // Time to wait between state transition checks, in seconds.
                    // Defaults to `10`.
                    //
                    retryDelay = 10

                    // Maximum number of attempts to verify state transition.
                    // Defaults to `60`.
                    // Maven Central release can currently take up to 20 minutes, so we increased this to 120
                    //
                    maxRetries = 120
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'edison-hal'
            groupId = 'de.otto.edison'
            from components.java

            pom {
                name = 'Edison Hal'
                url = 'https://github.com/otto-de/edison-hal'
                description = 'A library to parse and render application/hal+json documents in Java.'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'gsteinacker'
                        name = 'Guido Steinacker'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/otto-de/edison-hal.git'
                    developerConnection = 'scm:git:ssh://github.com/otto-de/edison-hal.git'
                    url = 'https://github.com/otto-de/edison-hal'
                }
            }
        }
    }

    repositories {
        maven {
            // Our settingsDirectory is the project root dir.
            // We want to 'publish' to the specified dir to have the artifacts uploaded with JReleaser from that location afterwards.
            url = layout.settingsDirectory.dir('build/staging-deploy')
        }
    }
}